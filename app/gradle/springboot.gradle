apply plugin: "org.springframework.boot"

configurations {
    bootRunConfig.extendsFrom compileClasspath

    configureEach {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            // Fix CVE-2023-20861
            if (details.requested.group == 'org.springframework') {
                details.useVersion '5.3.27'
            }

            // Fix CVE-2022-22965 / CVE-2023-20873 / CVE-2022-22965 / GHSA-36p3-wjmg-h94x / CVE-2023-20873 / CVE-2023-20883
            if (details.requested.group == 'org.springframework.boot') {
                details.useVersion '3.2.1'
            }

            // Fix CVE-2022-25857 / CVE-2022-38749 / CVE-2022-38749 / CVE-2022-38749 /  CVE-2022-38752 / CVE-2022-41854 / CVE-2022-1471
            if (details.requested.name == 'snakeyaml') {
                details.useVersion '2.0'
            }

            // Fix CVE-2019-20444 / CVE-2019-20445 / CVE-2019-16869 / CVE-2021-21290 / CVE-2021-21409 / CVE-2021-43797 // CVE-2022-24823
            if (details.requested.group == 'io.netty') {
                useVersion '4.1.94.Final'
            }

            // Fix CVE-2020-36518 / CVE-2020-36518 / CVE-2022-42004 / CVE-2022-42003 /
            if (details.requested.group == 'com.fasterxml.jackson.core') {
                useVersion '2.13.5'
            }

            // Fix CVE-2023-1370
            if (details.requested.group == 'net.minidev') {
                useVersion '2.4.9'
            }

            // Fix GHSA-jgvc-jfgh-rjvv
            if (details.requested.name == 'jose4j') {
                useVersion '0.9.3'
            }

            // Fix CVE-2022-45688
            if (details.requested.group == 'org.json') {
                useVersion '20230227'
            }

            // Fix CVE-2022-40151 / CVE-2022-40152
            if (details.requested.group == 'com.fasterxml.woodstox') {
                useVersion '6.4.0'
            }

            // Fix CVE-2023-39017
            if (details.requested.group == 'org.quartz-scheduler') {
                useVersion '2.4.0-rc2'
            }

            if (details.requested.group == 'org.springframework.security'
                    && details.requested.name != 'spring-security-rsa'
                    && details.requested.name != 'spring-security-config') {
                useVersion('5.7.10')
            }
        }
    }
}

dependencies {
    bootRunConfig "org.apereo.cas:cas-server-core:${project.'cas.version'}"
    bootRunConfig "org.apereo.cas:cas-server-core-logging:${project.'cas.version'}"
    bootRunConfig "org.apereo.cas:cas-server-core-web:${project.'cas.version'}"
    bootRunConfig "org.apereo.cas:cas-server-core-webflow:${project.'cas.version'}"
    bootRunConfig "org.apereo.cas:cas-server-core-cookie:${project.'cas.version'}"
    bootRunConfig "org.apereo.cas:cas-server-core-logout:${project.'cas.version'}"
    bootRunConfig "org.apereo.cas:cas-server-core-authentication:${project.'cas.version'}"
    bootRunConfig "org.apereo.cas:cas-server-core-validation:${project.'cas.version'}"
    bootRunConfig "org.apereo.cas:cas-server-core-audit:${project.'cas.version'}"
    bootRunConfig "org.apereo.cas:cas-server-core-tickets:${project.'cas.version'}"
    bootRunConfig "org.apereo.cas:cas-server-core-services:${project.'cas.version'}"
    bootRunConfig "org.apereo.cas:cas-server-core-util:${project.'cas.version'}"

    bootRunConfig "org.apereo.cas:cas-server-support-thymeleaf:${project.'cas.version'}"
    bootRunConfig "org.apereo.cas:cas-server-support-validation:${project.'cas.version'}"
    bootRunConfig "org.apereo.cas:cas-server-support-person-directory:${project.'cas.version'}"

    bootRunConfig "org.apereo.cas:cas-server-webapp-resources:${project.'cas.version'}"
    bootRunConfig "org.apereo.cas:cas-server-webapp-config:${project.'cas.version'}"
    bootRunConfig "org.apereo.cas:cas-server-webapp-init:${project.'cas.version'}"
    bootRunConfig "org.apereo.cas:cas-server-webapp-tomcat:${project.'cas.version'}"
    bootRunConfig "org.apereo.cas:cas-server-webapp-init-tomcat:${project.'cas.version'}"

    bootRunConfig "org.springframework.boot:spring-boot-devtools:${project.springBootVersion}"
}

sourceSets {
    bootRunSources {
        resources {
            srcDirs new File("//etc/cas/templates/"),
                    new File("${project.getProjectDir()}/src/main/resources/")
        }
    }
}

bootRun {
    classpath = configurations.bootRunConfig + sourceSets.main.compileClasspath + sourceSets.main.runtimeClasspath
    doFirst {
        sourceResources sourceSets.bootRunSources
        systemProperties = System.properties
    }

    def list = []
    list.add("-XX:TieredStopAtLevel=1")
    list.add("-Xverify:none")
    list.add("--add-modules")
    list.add("java.se")
    list.add("--add-exports")
    list.add("java.base/jdk.internal.ref=ALL-UNNAMED")
    list.add("--add-opens")
    list.add("java.base/java.lang=ALL-UNNAMED")
    list.add("--add-opens")
    list.add("java.base/java.nio=ALL-UNNAMED")
    list.add("--add-opens")
    list.add("java.base/sun.nio.ch=ALL-UNNAMED")
    list.add("--add-opens")
    list.add("java.management/sun.management=ALL-UNNAMED")
    list.add("--add-opens")
    list.add("jdk.management/com.sun.management.internal=ALL-UNNAMED")

    list.add("-XX:+UnlockExperimentalVMOptions")
    list.add("-XX:+EnableJVMCI")
    list.add("-XX:+UseJVMCICompiler")

    list.add("-Xrunjdwp:transport=dt_socket,address=5000,server=y,suspend=n")

    jvmArgs = list

    def appArgList = []
    args = appArgList
}

springBoot {
    buildInfo()
    mainClass = "org.apereo.cas.web.CasWebApplication"
}

bootBuildImage {
    imageName = "apereo/cas:${project.'cas.version'}"
}

bootWar {
    def executable = project.hasProperty("executable") && Boolean.valueOf(project.getProperty("executable"))
    if (executable) {
        logger.info "Including launch script for executable WAR artifact"
        launchScript()
    } else {
        logger.info "WAR artifact is not marked as an executable"
    }

    archiveFileName = "cas.war"
    archiveBaseName = "cas"

    entryCompression = ZipEntryCompression.STORED

    overlays {
        /*
            https://docs.freefair.io/gradle-plugins/current/reference/#_io_freefair_war_overlay
            Note: The "excludes" property is only for files in the war dependency.
            If a jar is excluded from the war, it could be brought back into the final war as a dependency
            of non-war dependencies. Those should be excluded via normal gradle dependency exclusions.
        */
        cas {
            from "org.apereo.cas:cas-server-webapp${project.appServer}:${project.'cas.version'}@war"
            provided = false
            excludes = [
                    "WEB-INF/lib/servlet-api-2*.jar",
                    "WEB-INF/lib/spring-boot*-3.1.2.jar", // upgraded
                    "WEB-INF/lib/spring-*-6.1.*.jar", // upgraded
                    "WEB-INF/lib/snakeyaml-1.31.jar", // upgraded
                    "WEB-INF/lib/snakeyaml-1.33.jar", // upgraded
                    "WEB-INF/lib/netty-*-4.1.80.Final.jar", // upgraded
                    "WEB-INF/lib/jackson-*-2.13.4.jar", // upgraded
                    "WEB-INF/lib/woodstox-core-6.3.1.jar", // upgraded
                    "WEB-INF/lib/tomcat-*-9.0.74.jar", // as we use an external tomcat server we can remove the embedded on
                    "WEB-INF/lib/json-smart-2.4.8.jar", // upgraded
                    "WEB-INF/lib/accessors-smart-2.4.8.jar", // upgraded
                    "WEB-INF/lib/jose4j-0.8.0.jar", // upgraded
                    "WEB-INF/lib/json-20160810.jar", // upgraded
                    "WEB-INF/lib/quartz-2.3*.jar", // upgraded
                    "WEB-INF/lib/spring-security-core-5.7.3.jar", // upgraded
                    "WEB-INF/lib/spring-security-cas-5.7.3.jar", // upgraded
                    "WEB-INF/lib/spring-security-crypto-5.7.3.jar", // upgraded
                    "WEB-INF/lib/spring-security-web-5.7.3.jar", // upgraded
            ]
        }
    }
}
